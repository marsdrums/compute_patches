#version 450

layout(set = 0, rgba32f, binding = 0) uniform image3D rendImg;
layout(set = 0, rgba32f, binding = 1) uniform image2D maskImg;

layout(binding = 2) uniform Cfg {
    float senseAngle;
    float turnAngle;
    float senseDist;
    float speed;
    uint frame;
    float maxPheromone;
    float evaporate;
};

layout(binding = 3) uniform slice {
    int readSlice;
    int writeSlice;
}pc;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

float sampleTerrain(ivec2 uv, ivec2 size, float weight){
	if(uv.x < 0 || uv.y < 0 || uv.x >= size.x || uv.y >= size.y){
		return 0.0;
	}
	return imageLoad(rendImg, ivec3(uv, pc.readSlice)).w * weight;
}

void main(){

    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(rendImg).xy;

    if (gid.x >= size.x || gid.y >= size.y) return;

    float mask = imageLoad(maskImg, gid - ivec2(960 - 250, 540 - 250)).r < 0.3 ? 1.0 : 0.3;

    
    float val = 0.0;
    val += sampleTerrain(gid + ivec2(-1,-1), size, 0.0625);
    val += sampleTerrain(gid + ivec2(+0,-1), size, 0.125);
    val += sampleTerrain(gid + ivec2(+1,-1), size, 0.0625);
    val += sampleTerrain(gid + ivec2(-1,+0), size, 0.125);
    val += sampleTerrain(gid + ivec2(+0,+0), size, 0.25);
    val += sampleTerrain(gid + ivec2(+1,+0), size, 0.125);
    val += sampleTerrain(gid + ivec2(-1,+1), size, 0.0625);
    val += sampleTerrain(gid + ivec2(+0,+1), size, 0.125);
    val += sampleTerrain(gid + ivec2(+1,+1), size, 0.0625);
    val = max(0.0, val - evaporate*mask);


    imageStore(rendImg, ivec3(gid, pc.writeSlice), vec4(0,0,0,val));
}