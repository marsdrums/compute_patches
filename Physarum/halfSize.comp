#version 450

layout(set = 0, rgba32f, binding = 0) uniform readonly image3D rendImg;
layout(set = 0, rgba32f, binding = 1) uniform readonly image2D norHeightImg;
layout(set = 0, rgba32f, binding = 2) uniform readonly image2D albedoImg;
layout(set = 0, rgba32f, binding = 3) uniform writeonly image3D half_rendImg;
layout(set = 0, rgba32f, binding = 4) uniform writeonly image2D half_norHeightImg;
layout(set = 0, rgba32f, binding = 5) uniform writeonly image2D half_albedoImage;

layout(binding = 6) uniform slice {
    int readSlice;
    int writeSlice;
}pc;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;


void main(){

    ivec2 gid = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(half_rendImg).xy;

    if (gid.x >= size.x || gid.y >= size.y) return;

    ivec2 gid00 = gid*2;
    ivec2 gid10 = gid00 + ivec2(1,0);
    ivec2 gid01 = gid00 + ivec2(0,1);
    ivec2 gid11 = gid00 + ivec2(1,1);

    vec4 val;

    val =  imageLoad(rendImg, ivec3(gid00, pc.readSlice));
    val += imageLoad(rendImg, ivec3(gid10, pc.readSlice));
    val += imageLoad(rendImg, ivec3(gid01, pc.readSlice));
    val += imageLoad(rendImg, ivec3(gid11, pc.readSlice));
    val *= 0.25;

    imageStore(half_rendImg, ivec3(gid, pc.readSlice), val);

    val =  imageLoad(norHeightImg, gid00);
    val += imageLoad(norHeightImg, gid10);
    val += imageLoad(norHeightImg, gid01);
    val += imageLoad(norHeightImg, gid11);
    val.xyz = normalize(val.xyz);

    imageStore(half_norHeightImg, gid, val);

    val =  imageLoad(albedoImg, gid00);
    val += imageLoad(albedoImg, gid10);
    val += imageLoad(albedoImg, gid01);
    val += imageLoad(albedoImg, gid11);
    val *= 0.25;

    imageStore(half_albedoImage, gid, val);

}