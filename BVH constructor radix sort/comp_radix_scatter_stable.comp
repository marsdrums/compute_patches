#version 460
layout(local_size_x = 128) in;

struct Key{
    uint morton;
    uint primID;
};

layout(std430, binding = 0) readonly buffer buff_in {
    Key keyIn[];
};

layout(std430, binding = 1) writeonly buffer buff_out {
    Key keyOut[];
};

layout(std430, binding = 2) readonly buffer buff_groupOffsets {
    uint groupOffsets[];
};

layout(std430, binding = 3) readonly buffer buff_binBase {
    uint binBase[];
};

layout(binding = 4) uniform cfg {
    uint N;
    uint shift;
};

shared uint sbin[128];

void main() {
    uint lid = gl_LocalInvocationID.x;
    uint gid = gl_GlobalInvocationID.x;
    uint wg  = gl_WorkGroupID.x;

    bool isActive = (gid < N);

    Key k;
    uint myBin = 0xFFFFFFFFu;

    if (isActive) {
        k = keyIn[gid];
        myBin = (k.morton >> shift) & 0xFFu;
    }

    sbin[lid] = myBin;
    barrier();

    if (!isActive) return;

    uint localRank = 0u;
    for (uint j = 0u; j < lid; ++j) {
        if (sbin[j] == myBin) localRank++;
    }

    uint dst = binBase[myBin] + groupOffsets[wg * 256u + myBin] + localRank;
    keyOut[dst] = k;
}
