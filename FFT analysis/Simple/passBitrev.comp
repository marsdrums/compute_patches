#version 450

layout(local_size_x = 256) in;

layout(std430, binding = 0) readonly buffer buff_real {
    float r[];
};

layout(std430, binding = 1) writeonly buffer buff_complex {
    vec2 A[];
};

layout(binding = 2) uniform samps_info {
    uint WS; //number of samples in this chunk
    uint log2WS; //log2 of the number of samples in this chunk
} cfg;

uint bitReverse32(uint v) {
    v = ((v & 0x55555555u) << 1)  | ((v & 0xAAAAAAAAu) >> 1);
    v = ((v & 0x33333333u) << 2)  | ((v & 0xCCCCCCCCu) >> 2);
    v = ((v & 0x0F0F0F0Fu) << 4)  | ((v & 0xF0F0F0F0u) >> 4);
    v = ((v & 0x00FF00FFu) << 8)  | ((v & 0xFF00FF00u) >> 8);
    v = (v << 16) | (v >> 16);
    return v;
}

void main() {
    uint i = gl_GlobalInvocationID.x;
    if (i >= cfg.WS) return;

    // bit-reversed index j in [0..N-1]
    uint j = bitReverse32(i) >> (32u - cfg.log2WS);

    // pack real -> complex
    A[j] = vec2(r[i], 0.0);
}
