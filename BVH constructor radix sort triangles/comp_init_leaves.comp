#version 460
layout(local_size_x = 256) in;

struct Key { uint morton; uint primID; };

struct Tri {
    vec4 v0;
    vec4 v1;
    vec4 v2;
};

layout(std430, binding = 0) readonly buffer buff_part    { Tri tri[];     };
layout(std430, binding = 1) readonly buffer buff_normPos { Key key[];     };
layout(std430, binding = 2) buffer buff_nodeChild        { ivec2 nodeChild[]; };
layout(std430, binding = 3) buffer buff_nodeAabbMin      { vec4 nodeAabbMin[]; };
layout(std430, binding = 4) buffer buff_nodeAabbMax      { vec4 nodeAabbMax[]; };
layout(std430, binding = 5) buffer buff_nodePrim         { int  nodePrim[]; };
layout(binding = 6) uniform cfg { uint N; };

void main() {
    uint i = gl_GlobalInvocationID.x;
    if (i >= N) return;

    uint prim = key[i].primID;
    Tri t = tri[prim];

    vec3 a = t.v0.xyz;
    vec3 b = t.v1.xyz;
    vec3 c = t.v2.xyz;

    vec3 tmin = min(a, min(b, c));
    vec3 tmax = max(a, max(b, c));

    nodeAabbMin[i] = vec4(tmin, 0.0);
    nodeAabbMax[i] = vec4(tmax, 0.0);

    nodePrim[i]  = int(prim);
    nodeChild[i] = ivec2(-1, -1);
}
